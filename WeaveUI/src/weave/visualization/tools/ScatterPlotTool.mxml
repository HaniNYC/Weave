<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/

-->
<SimpleVisTool xmlns="weave.visualization.tools.*" 
			   xmlns:mx="http://www.adobe.com/2006/mxml"
			   xmlns:ui="weave.ui.*" 
			   xmlns:settings="weave.ui.settings.*" xmlns:LinkableObjectList="weave.ui.LinkableObjectList.*"
			   xmlns:userControls="weave.ui.userControls.*"
			   layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.controls.TextArea;
			import mx.core.IToolTip;
			import mx.events.SliderEvent;
			import mx.managers.ToolTipManager;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.data.IAttributeColumn;
			import weave.api.data.IKeySet;
			import weave.api.data.IQualifiedKey;
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.linkSessionState;
			import weave.api.primitives.IBounds2D;
			import weave.api.registerLinkableChild;
			import weave.api.ui.IVisTool;
			import weave.compiler.StandardLib;
			import weave.core.LinkableBoolean;
			import weave.core.LinkableNumber;
			import weave.data.AttributeColumns.ColorColumn;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.AttributeColumns.FilteredColumn;
			import weave.primitives.Bounds2D;
			import weave.ui.AttributeSelectorPanel;
			import weave.utils.BitmapText;
			import weave.utils.ColumnUtils;
			import weave.utils.LinkableTextFormat;
			import weave.visualization.layers.PlotLayer;
			import weave.visualization.layers.SelectablePlotLayer;
			import weave.visualization.plotters.RegressionLinePlotter;
			import weave.visualization.plotters.ScatterPlotPlotter;
			import weave.visualization.plotters.styles.SolidFillStyle;

			WeaveAPI.registerImplementation(IVisTool, ScatterPlotTool, "Scatterplot");

			public var plotter:ScatterPlotPlotter = null;
			
			override protected function constructor():void
			{
				super.constructor();
				
				// lock dynamic objects into place
				plotter = initializePlotters(ScatterPlotPlotter, true);
				visualization.enableProbeLine(true, true);
				
				//BEGIN TEMPORARY SOLUTION
				visualization.marginLeftNumber.addGroupedCallback(this, updateAxisLabels);
				visualization.marginBottomNumber.addGroupedCallback(this, updateAxisLabels);
				getCallbackCollection(plotter).addGroupedCallback(this, updateAxisLabels, true);
				// END TEMPORARY SOLUTION
				
				visualization.enableZoomAndPan.value = false;
				
				xAxisLayer.axisPlotter.setLabelFunction(labelFunctionX);
				yAxisLayer.axisPlotter.setLabelFunction(labelFunctionY);
				
				var self:* = this;
				visualization.bottomMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.xColumn); })
				visualization.leftMarginClickCallbacks.addImmediateCallback(this, function():void { AttributeSelectorPanel.openToolSelector(self, plotter.yColumn); })
				
				visualization.enableAutoZoomXToNiceNumbers.value = true;
				visualization.enableAutoZoomYToNiceNumbers.value = true;
				
				getCallbackCollection(plotter).addGroupedCallback(this, handlePanelTitleChange, true);
				
				initColumnDefaults(plotter.xColumn, plotter.yColumn, colorColumn);
				
				plotter.xColumn.addGroupedCallback(this, updateProbeColumns);
				plotter.yColumn.addGroupedCallback(this, updateProbeColumns);
				colorColumn.addGroupedCallback(this, updateProbeColumns);
				
				plotter.enabledSizeBy.value = true;
				visualization.marginBottom.value = "80";
				visualization.marginTop.value    = "30";
				visualization.marginLeft.value   = "80";
				visualization.marginRight.value  = "30";
				
				initializeUndefinedLayers();				
				initRegressionLayer();
			}
			
			override public function getSelectableAttributeNames():Array
			{
				return ["X", "Y", "Color", "Size"];
			}
			override public function getSelectableAttributes():Array
			{
				return [plotter.xColumn, plotter.yColumn, colorColumn, plotter.radiusColumn];
			}
			
			private var _regressionLineLayer:PlotLayer ;
			private const REGRESSION_LAYER_NAME:String = "regressionLine";
			private var _regressionPlotter:RegressionLinePlotter = null;
			
			public function get regressionPlotter():RegressionLinePlotter
			{
				return _regressionPlotter;
			}
			
			public function set regressionPlotter(value:RegressionLinePlotter):void
			{
				_regressionPlotter = value;
			}
			
			private function initRegressionLayer():void
			{
				_regressionLineLayer = visualization.layers.requestObject(REGRESSION_LAYER_NAME, PlotLayer, true);
				_regressionPlotter = _regressionLineLayer.getDynamicPlotter().requestLocalObject(RegressionLinePlotter,true);
				linkSessionState(plotter.xColumn, _regressionPlotter.xColumn);
				linkSessionState(plotter.yColumn, _regressionPlotter.yColumn);
				_regressionPlotter.drawLine.value = false;				
			}
			
			
			/*BEGIN UNDEFINED LAYER CODE*/
					
			private var _undefinedXLayer:SelectablePlotLayer;
			private var _undefinedYLayer:SelectablePlotLayer;
			private var _undefinedXYLayer:SelectablePlotLayer;
			
			private var _undefinedXPlotter:ScatterPlotPlotter;
			private var _undefinedYPlotter:ScatterPlotPlotter;
			private var _undefinedXYPlotter:ScatterPlotPlotter;
									
			private const UNDEFINED_X_NAME:String = "undefinedX";
			private const UNDEFINED_Y_NAME:String = "undefinedY";
			private const UNDEFINED_XY_NAME:String = "undefinedXY";
			
			private var _tempPoint:Point = new Point();
			private const _tempBounds:Bounds2D = new Bounds2D();
			private const dataBounds:Bounds2D = new Bounds2D();
			private const _screenBounds:Bounds2D = new Bounds2D();						
			
			private function initializeUndefinedLayers():void
			{
				_undefinedXLayer = visualization.layers.requestObject(UNDEFINED_X_NAME,SelectablePlotLayer, true);
				_undefinedYLayer = visualization.layers.requestObject(UNDEFINED_Y_NAME, SelectablePlotLayer, true);
				_undefinedXYLayer = visualization.layers.requestObject(UNDEFINED_XY_NAME,SelectablePlotLayer, true);
				
				_undefinedXPlotter = _undefinedXLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
				_undefinedYPlotter = _undefinedYLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
				_undefinedXYPlotter = _undefinedXYLayer.getDynamicPlotter().requestLocalObject(ScatterPlotPlotter, true);
				
				for each(var _plotter:ScatterPlotPlotter in [_undefinedXPlotter, _undefinedYPlotter, _undefinedXYPlotter])
				{
					linkSessionState(plotter.colorColumn, _plotter.colorColumn);
					linkSessionState(plotter.alphaColumn, _plotter.alphaColumn);
					linkSessionState(plotter.radiusColumn, _plotter.radiusColumn);					
					linkSessionState(plotter.defaultScreenRadius, _plotter.defaultScreenRadius);
					linkSessionState(plotter.enabledSizeBy, _plotter.enabledSizeBy);
					linkSessionState(plotter.maxScreenRadius, _plotter.maxScreenRadius);
					linkSessionState(plotter.minScreenRadius, _plotter.minScreenRadius);
				} 	
				
				linkSessionState(plotter.xColumn, _undefinedYPlotter.xColumn);
				linkSessionState(plotter.yColumn, _undefinedXPlotter.yColumn);
				
				for each(var column:IAttributeColumn in getSelectableAttributes())
				{
					column.addGroupedCallback(this, setUndefinedPlotterKeySource, true);
				}
				
				getCallbackCollection(visualization.zoomBounds).addImmediateCallback(this, invalidateDisplayList);
				
				visualization.marginTopNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.marginBottomNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.marginRightNumber.addImmediateCallback(this, invalidateDisplayList);
				visualization.marginLeftNumber.addImmediateCallback(this, invalidateDisplayList);
				
				visualization.gridLineAlpha.addImmediateCallback(this, invalidateDisplayList);
				visualization.gridLineColor.addImmediateCallback(this, invalidateDisplayList);
				visualization.gridLineThickness.addImmediateCallback(this, invalidateDisplayList);
				
				for each (var layer:SelectablePlotLayer in [_undefinedXLayer, _undefinedYLayer, _undefinedXYLayer])
				{					
					layer.showMissingRecords(true);
					
					linkSessionState(plotLayer.backgroundIsVisible, layer.backgroundIsVisible);
					linkSessionState(plotLayer.layerIsVisible, layer.layerIsVisible);
					linkSessionState(plotLayer.layerIsSelectable, layer.layerIsSelectable);					
				} 																		
				
				addEventListener(MouseEvent.MOUSE_MOVE, displayToolTip);
				addEventListener(MouseEvent.MOUSE_OUT, destroyToolTip);
			}						
			
			private function setUndefinedPlotterKeySource():void
			{
				var recordKeys:Array = plotter.keySet.keys;
				var missingXkeys:Array = [];
				var missingYkeys:Array = [];
				var missingXYkeys:Array = [];
				var missingX:Boolean;
				var missingY:Boolean;
				
				for each(var key:IQualifiedKey in recordKeys)	
				{
					missingX = !plotter.xColumn.containsKey(key);
					missingY = !plotter.yColumn.containsKey(key);
					
					if(missingX)
					{
						if(missingY)
							missingXYkeys.push(key);
						else 
							missingXkeys.push(key);
					}
					else if(missingY)
					{
						missingYkeys.push(key);
					}
				}
				_undefinedXPlotter.setCustomKeySource(missingXkeys);
				_undefinedYPlotter.setCustomKeySource(missingYkeys);
				_undefinedXYPlotter.setCustomKeySource(missingXYkeys);
			}
			
			
			override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
			{
				super.updateDisplayList(unscaledWidth, unscaledHeight);
				updateUndefinedPlotterBounds();
			}
			
			
			private function updateUndefinedPlotterBounds():void
			{
				var b:IBounds2D = _tempBounds;
				plotLayer.getScreenBounds(b);
				if (b.isEmpty())
				{
					callLater(updateUndefinedPlotterBounds);
					return;
				}
				
				var graphics:Graphics = visualization.graphics;
				graphics.clear();
				graphics.lineStyle(visualization.gridLineThickness.value, visualization.gridLineColor.value, visualization.gridLineAlpha.value);
				
				var marginRight:Number = visualization.marginRightNumber.value;
				
				// right margin; undefined x
				plotLayer.getScreenBounds(b);
				graphics.moveTo(b.getXMax() + marginRight / 2, b.getYMin());
				graphics.lineTo(b.getXMax() + marginRight / 2, b.getYMax());
				b.setXMin(b.getXMax() + marginRight);
				_undefinedXLayer.hack_lockScreenBounds(b);
				
				// top margin; undefined y
				plotLayer.getScreenBounds(b);
				graphics.moveTo(b.getXMin(), b.getYMax() / 2);
				graphics.lineTo(b.getXMax(), b.getYMax() / 2);
				b.setYMin(0);
				_undefinedYLayer.hack_lockScreenBounds(b);
				
				// top-right corner; undefined x and y
				plotLayer.getScreenBounds(b);
				b.setMinCoords(b.getXMax() + marginRight, 0);
				_undefinedXYLayer.hack_lockScreenBounds(b);
			}
			
			
			private var _toolTip:IToolTip = null;
			private function displayToolTip(event:MouseEvent):void
			{
				destroyToolTip(event);
				_tempPoint = this.globalToContent(new Point(event.stageX, event.stageY));
								
				var probeExists:Boolean = plotLayer.probeFilter.internalObject is IKeySet &&
					(plotLayer.probeFilter.internalObject as IKeySet).keys.length > 0;

				var selectionExists:Boolean = plotLayer.selectionFilter.internalObject is IKeySet &&
					(plotLayer.selectionFilter.internalObject as IKeySet).keys.length > 0;
				
				if(probeExists || selectionExists) return;
				
				_undefinedXLayer.getScreenBounds(_tempBounds);				
				if(_tempBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip("undefined x",_tempPoint.x, _tempPoint.y);	
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}
				_undefinedYLayer.getScreenBounds(_tempBounds);
				if(_tempBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip("undefined y",_tempPoint.x, _tempPoint.y);
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}
				_undefinedXYLayer.getScreenBounds(_tempBounds);
				if(_tempBounds.containsPoint(_tempPoint))
				{
					_tempPoint = this.contentToGlobal(_tempPoint);
					_toolTip = ToolTipManager.createToolTip("undefined x,y",_tempPoint.x, _tempPoint.y);
					visualization.constrainToolTipsToStage(_toolTip);
					return;
				}   
			}
			
			private function destroyToolTip(event:MouseEvent):void
			{
				if(_toolTip != null) 
				{
					ToolTipManager.destroyToolTip(_toolTip);
					_toolTip = null;
				}
			}
			/*END UNDEFINED LAYER CODE*/
			
			public const includeToolAttributesInProbe:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(true), updateProbeColumns);
			
			private function updateProbeColumns():void
			{
				if(includeToolAttributesInProbe.value)
				{
					visualization.additionalProbeColumns = [];
					if (plotter.xColumn.internalColumn)
						visualization.additionalProbeColumns.push(plotter.xColumn.internalColumn);
					if (plotter.yColumn.internalColumn)
						visualization.additionalProbeColumns.push(plotter.yColumn.internalColumn);
					if (colorColumn.internalColumn)
						visualization.additionalProbeColumns.push(colorColumn.internalColumn);
					if (visualization.additionalProbeColumns.length == 0)
						visualization.additionalProbeColumns = null ;
				}
				else visualization.additionalProbeColumns = null;
			}
			
			override protected function get defaultPanelTitle():String
			{
				return "Scatter Plot of " + ColumnUtils.getTitle(plotter.xColumn) + " -vs- " + ColumnUtils.getTitle(plotter.yColumn);
			}
			
			override protected function childrenCreated():void
			{
				super.childrenCreated();

				plotter.colorColumn.addImmediateCallback(this, handleColorColumnChange, true);
				visualization.marginLeftNumber.addGroupedCallback(this, updateEquation);
				visualization.marginBottomNumber.addGroupedCallback(this, updateEquation);
				getCallbackCollection(_regressionPlotter).addGroupedCallback(this, updateEquation, true);
				showColorPicker.addGroupedCallback(this, updateColorPicker);
				overrideColor.addGroupedCallback(this, updateColorPicker);
				
				getCallbackCollection(LinkableTextFormat.defaultTextFormat).addGroupedCallback(this, updateEquationStyle, true);
			}
			
			private function updateEquationStyle():void
			{
				LinkableTextFormat.defaultTextFormat.copyToStyle(equationText);
			}
			
			private function updateEquation():void
			{
				if ( _regressionPlotter.drawLine.value )
				{
					if (equationText.parent != visCanvas)
						visCanvas.addChild(equationText);
					var slope:Number = StandardLib.roundSignificant(_regressionPlotter.getSlope(), 4);
					var intercept:Number = StandardLib.roundSignificant(_regressionPlotter.getIntercept(), 4);
					equationText.text = "slope = " + slope + "\nintercept = " + intercept;
					equationText.validateNow();
					// severely hacked
					equationText.width = visualization.marginLeftNumber.value * 2;
					equationText.height = Math.min(equationText.textHeight + 10, visualization.marginBottomNumber.value);
				}
				else
				{ 
					if (equationText.parent)
						equationText.parent.removeChild(equationText);
				}
			}
			private function updateAxisLabels():void
			{
				visualization.bottomMarginColumn = plotter.xColumn;
				visualization.leftMarginColumn = plotter.yColumn;
				
				xAxisLayer.axisPlotter.setSideAxisName(
					ColumnUtils.getTitle(plotter.xColumn), 
					0,   
					0, visualization.marginBottomNumber.value - 3, 
					BitmapText.VERTICAL_ALIGN_BOTTOM
				);
				
				yAxisLayer.axisPlotter.setSideAxisName(
					ColumnUtils.getTitle(plotter.yColumn), 
					-90, 
					-visualization.marginLeftNumber.value, 0, 
					BitmapText.VERTICAL_ALIGN_TOP
				);
			}
			
			public function labelFunctionX(value:Number):String
			{
				return ColumnUtils.deriveStringFromNumber(plotter.xColumn, value);
			}
			public function labelFunctionY(value:Number):String
			{
				return ColumnUtils.deriveStringFromNumber(plotter.yColumn, value);
			}
			
			public function get colorColumn():DynamicColumn
			{
				return Weave.defaultColorDataColumn.internalDynamicColumn;
			}			
			
			private function updateColorPicker():void
			{
				if (!parent)
				{
					callLater(updateColorPicker);
					return;
				}
				
				if ( showColorPicker.value )
				{
					plotter.colorColumn.defaultValue.value = overrideColor.value;
					plotter.colorColumn.internalDynamicColumn.removeObject();
				}
				else
				{
					plotter.colorColumn.defaultValue.value = NaN;
					plotter.colorColumn.internalDynamicColumn.requestGlobalObject(_prevGlobalColorColumnName, ColorColumn, false);
				}
			}
			
			private var _prevGlobalColorColumnName:String = null;
			private function handleColorColumnChange():void
			{
				var newName:String = plotter.colorColumn.internalDynamicColumn.globalName;
				if (newName)
					_prevGlobalColorColumnName = newName;
			}
			
			public const showColorPicker:LinkableBoolean = registerLinkableChild(this, new LinkableBoolean(false));
			public const overrideColor:LinkableNumber = registerLinkableChild(this, new LinkableNumber());
				
		]]>
	</mx:Script>
	<mx:TextArea id="equationText"
		editable="false"
		verticalScrollPolicy="off"
		horizontalScrollPolicy="off"
		wordWrap="false"
		backgroundAlpha="0"
		borderThickness="0"
		bottom="0"
		left="0"/>
</SimpleVisTool>
