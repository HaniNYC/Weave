<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<mx:VBox 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	xmlns="weave.editors.*"
	verticalScrollPolicy="off"
	horizontalScrollPolicy="off"
	creationPolicy="all"
	implements="weave.api.ui.IDataSourceEditor" xmlns:ui="weave.ui.*"
	>
	<mx:Label id="editorTitle" fontWeight="bold" text="{lang( (_dataSource ? 'Edit' : 'Add') + ' CKAN Data Source' )}"/>
	<ui:Indent label="{lang('Source Name *')}">
		<mx:TextInput id="sourceName" width="200"/>
	</ui:Indent>
	<ui:Indent label="{lang('Source URL *')}">
		<ui:TextInputWithPrompt id="url" width="300" prompt="http://www.example.com/" text="http://demo.ckan.org"/>
	</ui:Indent>
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableObject;
			import weave.api.data.IDataSource;
			import weave.compiler.StandardLib;
			import weave.data.DataSources.CKANDataSource;
			
			override protected function createChildren():void
			{
				super.createChildren();
				sourceName.text = WeaveAPI.globalHashMap.generateUniqueName("CKANDataSource");
			}
			
			[Bindable] private var _dataSource:CKANDataSource = null;
			public function setTarget(object:ILinkableObject):void
			{
				_dataSource = object as CKANDataSource;
				
				if (!parent)
				{
					callLater(setTarget, arguments);
					return;
				}
				
				if (_dataSource)
				{
					sourceName.text = WeaveAPI.globalHashMap.getName(_dataSource);
					url.text = _dataSource.url.value;
					sourceName.editable = false;
					sourceName.toolTip = lang("Source name is not editable once added to the session. If needed, delete the source and add it again with the new name.");
				}
			}
			
			public function hasPendingChanges():Boolean
			{
				return StandardLib.asString(_dataSource.url.value) != StandardLib.asString(url.text);
			}
			
			public function applyChanges():void
			{
				_dataSource.url.value = url.text;
			}
			
			public function createDataSource():IDataSource
			{
				if (!sourceName.text || (!url.text))
				{
					Alert.show(lang("Please fill all required fields"), lang("Error"));
					return null;
				}
				if (WeaveAPI.globalHashMap.getObject(sourceName.text))
				{
					Alert.show(lang("Source Name already being used. Please give a different name"), lang("Error"));
					return null;
				}
				
				_dataSource = WeaveAPI.globalHashMap.requestObject(sourceName.text, CKANDataSource, false);
				if (!_dataSource)
				{
					Alert.show(lang("Unexpected problem. Unable to add the data source."));
					return null;
				}
				applyChanges();
				return _dataSource;
			}
		]]>
	</mx:Script>
</mx:VBox>